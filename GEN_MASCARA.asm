global _gen_mascara

section .data
align 16
masc1 db 00h, 0FFh, 0FFh, 0FFh, 01h,0FFh, 0FFh, 0FFh, 02h, 0FFh, 0FFh, 0FFh, 03h,0FFh, 0FFh, 0FFh  ;--> 0FFF3h|FFF2h|FFF1h|FFF0h
masc2 db 00h, 04h, 08h, 0Ch, 0FFh, 0FFh, 0FFh, 0FFh, 0FFh, 0FFh, 0FFh, 0FFh,  0FFh, 0FFh, 0FFh, 0FFh ;--> FFFFh|FFFFh|FFFFh|3210h
section .text
;UBICACIÓN DE LOS PARAMETROS EN LA PILA

;[EBP+8] VECTOR R
;[EBP+12] VECTOR G
;[EBP+16] VECTOR B
;[EBP+20] LARGO DEL VECTOR
;[EBP+24] COLOR R TRANSPARENTE
;[EBP+28] COLOR G TRANSPARENTE
;[EBP+32] COLOR B TRANSPARENTE
;[EBP+36] FLOAT T DE TOLERANCIA AL COLOR
;[EBP+40] VECTOR MASCARA

_gen_mascara:
    PUSH EBP			;ARMO CONTEXTO
    MOV EBP, ESP
    
    MOV EAX, [EBP+8]            ;GUARDO EN EAX EL PUNTERO AL VECTOR R
    MOV EBX, [EBP+12]           ;GUARDO EN EBX EL PUNTERO AL VECTOR G
    MOV EDX, [EBP+16]           ;GUARDO EN EDX EL PUNTERO AL VECTOR B
	
    MOV ESI, [EBP+20]           ;GUARDO EL LARGO DEL VECTOR EN ESI!
    SHR ESI, 2                  ;DIVIDO ESI POR 4 --> ME DA LA CANTIDAD DE ITERACIONES
	
    MOV EDI, [EBP+40]           ;GUARDO EN EDI EL PUNTERO AL VECTOR MASCARA RESULTADO

    ;YA ARMAMOS EL CONTEXTO, AHORA VAMOS A COMENZAR A PROCESAR LA IMAGEN!
    MOV ECX, 0                  ;INICIO EL CONTADOR EN 0!   
        
    ;AGARRO LOS COMPONENTES
    ;ARMO T
    MOVD XMM4, [EBP+36]         ;GUARDO EN XMM3 EL FLOAT DE PRECISIÓN SIMPLE T
    PSHUFD XMM4, XMM4, 00000000 ;APLICO LA MASCARA AL VALOR T --> 000T|000T|000T|000T
 
    ;ARMO LOS COLORES TRANSPARENTES
    MOVD XMM7, [EBP+24]
    PSHUFD XMM7, XMM7, 00000000 ;APLICO LA MASCARA AL VALOR R TRANSPARENTE --> 000RT|000RT|000RT|000RT
    CVTDQ2PS XMM7, XMM7         ;CONVIERTO LOS VALORES DEL Rt EN PUNTO FLOTANTE DE PRECISIÓN SIMPLE
    
    MOVD XMM6, [EBP+28]
    PSHUFD XMM6, XMM6, 00000000 ;APLICO LA MASCARA AL VALOR G TRANSPARENTE --> 000GT|000GT|000GT|000GT
    CVTDQ2PS XMM6, XMM6         ;CONVIERTO LOS VALORES DEL Gt EN PUNTO FLOTANTE DE PRECISIÓN SIMPLE
    
    MOVD XMM5, [EBP+32]
    PSHUFD XMM5, XMM5, 00000000 ;APLICO LA MASCARA AL VALOR B TRANSPARENTE --> 000BT|000BT|000BT|000BT
    CVTDQ2PS XMM5, XMM5         ;CONVIERTO LOS VALORES DEL Bt EN PUNTO FLOTANTE DE PRECISIÓN SIMPLE
    
    XOR ECX, ECX
    
ciclar:
    CMP ECX, ESI
    JE salir
     
    ;EMPIEZO A AGARRAR LOS PARAMETROS DE LA IMAGEN
    MOVD XMM1, [EAX+ECX*4]      ;GUARDO 4 BYTES DEL VECTOR R
    MOVD XMM2, [EBX+ECX*4]      ;GUARDO 4 BYTES DEL VECTOR G
    MOVD XMM3, [EDX+ECX*4]      ;GUARDO 4 BYTES DEL VECTOR B
    
    PSHUFB XMM1, [masc1]        ;APLICO MASCARA A R --> 000V3|000V2|000V1|000V0
    PSHUFB XMM2, [masc1]        ;APLICO MASCARA A G --> 000V3|000V2|000V1|000V0
    PSHUFB XMM3, [masc1]        ;APLICO MASCARA A B --> 000V3|000V2|000V1|000V0
    
    CVTDQ2PS XMM1, XMM1         ;CONVIERTO LOS VALORES DEL R EN PUNTO FLOTANTE DE PRECISIÓN SIMPLE
    CVTDQ2PS XMM2, XMM2         ;CONVIERTO LOS VALORES DEL G EN PUNTO FLOTANTE DE PRECISIÓN SIMPLE
    CVTDQ2PS XMM3, XMM3         ;CONVIERTO LOS VALORES DEL B EN PUNTO FLOTANTE DE PRECISIÓN SIMPLE
    
    ;OPERACIÓN: D = RAIZ[ (R - RT )^2 + (G - GT)^2 + (B - BT)^2 ]
     SUBPS XMM1, XMM7           ;RESTO R - RT
     SUBPS XMM2, XMM6           ;RESTO G - GT
     SUBPS XMM3, XMM5           ;RESTO B - BT
     
     MULPS XMM1, XMM1           ;ELEVO AL CUADRADO R - RT
     MULPS XMM2, XMM2           ;ELEVO AL CUADRADO G -GT
     MULPS XMM3, XMM3           ;ELEVO AL CUADRADO B -BT
     
     ADDPS XMM1, XMM2           ;SUMO Ri + Gi
     ADDPS XMM1, XMM3           ;SUMO Ri + Ri + Bi
     
     SQRTPS XMM1, XMM1          ;APLICO LA RAÍZ CUADRADA ORIGEN XMM7 DESTINO XMM7
   
     CMPPS XMM1, XMM4, 1        ;HACEMOS LA COMPARACION SI D<T Y ESTO NOS GENERA UNA MASCARA EN XMM4!
     
     PSHUFB XMM1, [masc2]       ;APLICO MASCARA AL RESULTADO QUEDARIA ALGO COMO  --> 00000|00000|00000|FF00FF00
     MOVD [EDI+ECX*4], XMM1     ;GUARDO EL PROCESAMIENTO EN EL VECTOR RESULTADO!
     
    INC ECX   
    JMP ciclar
    
salir:
    MOV ESP, EBP
    POP EBP
    RET